apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    signingConfigs {
    }
    compileSdkVersion 28
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def value = 0

        List<String> runTasks = gradle.startParameter.getTaskNames();

        def versionMajor = 1
        def versionMinor = 0
        for (String item : runTasks) {
            if (item.contains("assemble") && item.contains("Release")) {
                value = 1
            }
        }

        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        def versionNumber = versionProps['VERSION_CODE'].toInteger() + value

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_CODE'] = versionNumber.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "infosys.com.kotlinmvvmsample"
            minSdkVersion rootProject.ext.minSdkVersion
            targetSdkVersion rootProject.ext.targetSdkVersion
            versionCode versionNumber
            versionName "${versionMajor}.${versionMinor}.${versionNumber}.${versionPatch} (${versionBuild}) Release"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }


        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "${versionName}-Android_Assignment.apk"
            }
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }
    signingConfigs {
        release {
            keyAlias 'android'
            keyPassword 'android'
            storeFile file('../app/release/android_assignment.keystore')
            storePassword 'android'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig getSigningConfig()

        }
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig getSigningConfig()

        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constrainLayout"

    // card view
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"

    implementation "android.arch.lifecycle:extensions:$rootProject.lifeCycleVersion"
    implementation "android.arch.lifecycle:runtime:$rootProject.lifeCycleVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$rootProject.lifeCycleVersion"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit2Version"

    // Picasso
    implementation "com.squareup.picasso:picasso:$rootProject.picassoVersion"

    //Gson
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit2Version"

    // Room
    implementation "android.arch.persistence.room:runtime:$rootProject.roomVersion"
    kapt "android.arch.persistence.room:compiler:$rootProject.roomVersion"

    androidTestImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"


    //Testing only dependencies
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoCoreVersion"
    androidTestImplementation "org.mockito:mockito-android:$rootProject.mockitoVersion"
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.rulesVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.runnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoCore"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$rootProject.espressoContrib") {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
}
kapt {
    generateStubs = true
}